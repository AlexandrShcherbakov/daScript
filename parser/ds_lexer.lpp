%{
    #include "platform.h"
    #include "ast.h"
    #include "ds_parser.hpp"
    
    using namespace std;
    using namespace yzg;
    
    #define YY_NO_INPUT
    
    void yyerror(const string & error);
    
    int current_line_indent = 0;
    int indent_level = 0;
    int tab_size = 4;   // only 2,4 or 8
    int nested_parentheses = 0;
    int nested_curly_braces = 0;
    int yycolumn = 1;
    bool need_oxford_comma = true;
    
    #define YY_USER_ACTION \
        yylloc.first_line = yylloc.last_line = yylineno; \
        yylloc.first_column = yycolumn; \
        yylloc.last_column = yycolumn + yyleng - 1; \
        yycolumn += yyleng;
        
%}

%option noyywrap
%option never-interactive
%option nounistd
%option yylineno

/* %option debug */

%x indent
%s normal

%%

<indent>" "             { current_line_indent++; }
<indent>"\t"            { current_line_indent = (current_line_indent + tab_size) & (tab_size-1);  }
<indent>(\/\/.*)*\n     { current_line_indent = 0; need_oxford_comma = true; yycolumn = 1; }
<indent>.        {
    unput(*yytext);
    if (current_line_indent > indent_level*tab_size ) {
        indent_level++;
        #ifdef FLEX_DEBUG
        cout << "emit {\n";
        #endif
        return '{';
    } else if (current_line_indent < indent_level*tab_size ) {
        indent_level--;
        #ifdef FLEX_DEBUG
        cout << "emit }\n";
        #endif
        yycolumn--;
        return '}';
    } else {
        BEGIN(normal);
    }
}
<indent><<EOF>> {
    if ( indent_level ) {
        indent_level--;
        unput('\r');
        #ifdef FLEX_DEBUG
        cout << "emit }\n";
        #endif
        return '}';
    } else {
        return 0;
    }
}

<normal>"foreach"                                       need_oxford_comma = false; return _FOREACH;
<normal>"in"                                            need_oxford_comma = false; return _IN;
<normal>"while"                                         need_oxford_comma = false; return _WHILE;
<normal>"if"                                            need_oxford_comma = false; return _IF;
<normal>"elif"                                          need_oxford_comma = false; return _ELIF;
<normal>"else"                                          need_oxford_comma = false; return _ELSE;
<normal>"def"                                           need_oxford_comma = false; return _DEF;
<normal>"let"                                           need_oxford_comma = false; return _LET;
<normal>"struct"                                        need_oxford_comma = false; return _STRUCT;
<normal>"null"                                          return _NULL;
<normal>"return"                                        return _RETURN;
<normal>"assert"                                        return _ASSERT;
<normal>"debug"                                         return _DEBUG;
<normal>"type"                                          return _TYPE;
<normal>"new"                                           return _NEW;
<normal>"sizeof"                                        return _SIZEOF;
<normal>"true"                                          return _TRUE;
<normal>"false"                                         return _FALSE;
<normal>"bool"                                          return _BOOL;
<normal>"void"                                          return _VOID;
<normal>"string"                                        return _STRING;
<normal>"int"                                           return _INT;
<normal>"int2"                                          return _INT2;
<normal>"int3"                                          return _INT3;
<normal>"int4"                                          return _INT4;
<normal>"uint"                                          return _UINT;
<normal>"uint2"                                         return _UINT2;
<normal>"uint3"                                         return _UINT3;
<normal>"uint4"                                         return _UINT4;
<normal>"float"                                         return _FLOAT;
<normal>"float2"                                        return _FLOAT2;
<normal>"float3"                                        return _FLOAT3;
<normal>"float4"                                        return _FLOAT4;
<normal>[_[:alpha:]][_[:alnum:]]*                       yylval.s = new string(yytext);  return NAME;    // TODO: track allocations
L?\"(\\.|[^\\"])*\"                                     yylval.s = new string(yytext+1,yytext+strlen(yytext)-1);  return STRING;  
<normal>0[xX][0-9a-fA-F]+                               return sscanf(yytext, "%u", &yylval.ui)!=1 ? LEXER_ERROR : UNSIGNED_INTEGER;
<normal>([0-9]*)?\.[0-9]+([eE][+\-]?[0-9]+)?(f|F)?      return sscanf(yytext, "%lf", &yylval.d)!=1 ? LEXER_ERROR : DOUBLE;
<normal>[0-9][0-9]*\.[0-9]+?([eE][+\-]?[0-9]+)?(f|F)?   return sscanf(yytext, "%lf", &yylval.d)!=1 ? LEXER_ERROR : DOUBLE;
<normal>[0-9]+(f|F)                                     return sscanf(yytext, "%lf", &yylval.d)!=1 ? LEXER_ERROR : DOUBLE;
<normal>[0-9]+[eE][+\-]?[0-9]+(f|F)?                    return sscanf(yytext, "%lf", &yylval.d)!=1 ? LEXER_ERROR : DOUBLE;
<normal>[0-9]+(l|L)?                                    return sscanf(yytext, "%i", &yylval.i)!=1 ? LEXER_ERROR : INTEGER;
<normal>0[xX][0-9a-fA-F]+(u|U)                          return sscanf(yytext, "%u", &yylval.ui)!=1 ? LEXER_ERROR : UNSIGNED_INTEGER;
<normal>[0-9]+(u|U)                                     return sscanf(yytext, "%u", &yylval.ui)!=1 ? LEXER_ERROR : UNSIGNED_INTEGER;
<normal>\)                                  {
    if ( !nested_parentheses )
        yyerror("mismatching parentheses");
    nested_parentheses --;
    return ')';
}
<normal>\(                                  {
    nested_parentheses ++;
    return '(';
}
<normal>\}                                  {
    if ( !nested_curly_braces )
        yyerror("mismatching curly braces");
    nested_curly_braces --;
    return '}';
}
<normal>\{                                  {
    nested_curly_braces ++;
    return '{';
}
<normal>"\-\>"                              return ARROW;
<normal>"\>\>"                              return SHR;
<normal>"\<\<"                              return SHL;
<normal>"\+\="                              return ADDEQU;
<normal>"\-\="                              return SUBEQU;
<normal>"\/\="                              return DIVEQU;
<normal>"\*\="                              return MULEQU;
<normal>"\%\="                              return MODEQU;
<normal>"\>\>\="                            return SHREQU;
<normal>"\<\<\="                            return SHLEQU;
<normal>"\&\="                              return ANDEQU;
<normal>"\|\="                              return OREQU;
<normal>"\^\="                              return XOREQU;
<normal>"\+\+"                              return ADDADD;
<normal>"\-\-"                              return SUBSUB;
<normal>"\<\="                              return LEEQU;
<normal>"\>\="                              return GREQU;
<normal>"\=\="                              return EQUEQU;
<normal>"\!\="                              return NOTEQU;
<normal>[ \t\r]                             /* skip white space */
<normal>(\/\/.*)*\n                        {
    yycolumn = 1;
    if  ( !nested_parentheses && !nested_curly_braces ) {
        bool ns = (current_line_indent!=0) && need_oxford_comma;
        current_line_indent = 0;
        need_oxford_comma = true;
        BEGIN(indent);
        #ifdef FLEX_DEBUG
        cout << "emit ;\n";
        #endif
        if ( ns )
            return ';';
    }
}
<normal>.                                   return *yytext;

%%

extern int yydebug;

void yybegin(const char * str)
{
    yydebug = 0;
    need_oxford_comma = true;
    yycolumn = 1;
    current_line_indent = 0;
    indent_level = 0;
    nested_curly_braces = 0;
    nested_curly_braces = 0;
    BEGIN(normal);
    yy_scan_string(str);
}
