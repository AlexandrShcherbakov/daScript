%{
    #include "daScript/misc/platform.h"
    #include <inttypes.h>
    #include "daScript/ast/ast.h"
    #include "ds_parser.hpp"

    #ifndef SCNi64
      #define SCNi64       "lli"
    #endif
    #ifndef SCNu64
      #define SCNu64       "llu"
    #endif
    #ifndef SCNx64
      #define SCNx64       "llx"
    #endif
    
    using namespace das;
    
    #define YY_NO_INPUT
    
    void das_yyerror(const string & error, CompilationError cerr = CompilationError::syntax_error);
    
    int das_current_line_indent = 0;
    int das_indent_level = 0;
    int das_def_tab_size = 4;
    int das_tab_size = das_def_tab_size;   // only 2,4 or 8
    int das_nested_parentheses = 0;
    int das_nested_curly_braces = 0;
	int das_nested_square_braces = 0;
    int das_nested_sb = 0;
    int das_yycolumn = 1;
    int das_c_style_depth = 0;
    bool das_need_oxford_comma = true;
    bool das_in_normal = false;
    vector<int> das_line_no;
    unordered_set<string> das_already_include;
    
    #define YY_USER_ACTION \
        das_yylloc.first_line = das_yylloc.last_line = yylineno; \
        das_yylloc.first_column = das_yycolumn; \
        das_yylloc.last_column = das_yycolumn + yyleng - 1; \
        das_yycolumn += yyleng;
        
%}

%option prefix="das_yy"
%option noyywrap
%option never-interactive
%option nounistd
%option yylineno

/* %option debug */

%x indent
%x strb
%x c_comment
%x include
%s normal

%%

<indent,normal>\#[0-9]+,[0-9]+,\"[^\"]+\"\# {
    string txt = yytext;
    int lRow, lCol;
    char lFile[256];
    if ( sscanf ( yytext, "#%i,%i,\"%255s\"#", &lRow, &lCol, lFile )==3 ) {
        lFile[strlen(lFile)-2] = 0;
        auto cfi = g_FileAccessStack.back();
        string incFileName = g_Access->getIncludeFileName(cfi->name,lFile);
        auto info = g_Access->getFileInfo(incFileName);
        if ( !info ) {
            das_yyerror("can't open "+incFileName);
        } else {
            g_FileAccessStack.pop_back();
            g_FileAccessStack.push_back(info);
            yylineno = lRow;
            das_yycolumn = lCol;
        }
    } else {
        das_yyerror ("can't process line directive " + string(yytext),
            CompilationError::invalid_line_directive); return LEXER_ERROR;
    }
}
<indent>"*/"                        das_yyerror ("Unexpected */", CompilationError::unexpected_close_comment); return LEXER_ERROR;
<indent>"/*"                        BEGIN(c_comment); das_c_style_depth = 1; das_in_normal = false;
<normal>"*/"                        das_yyerror ("Unexpected */", CompilationError::unexpected_close_comment); return LEXER_ERROR;
<normal>"/*"                        BEGIN(c_comment); das_c_style_depth = 1; das_in_normal = true;
<c_comment>"/*"                     das_c_style_depth ++;
<c_comment>"*/" {
    das_c_style_depth --;
    if ( das_c_style_depth==0 ) {
        if ( das_in_normal ) {
            BEGIN(normal);
        } else {
            BEGIN(indent);
        }
    }
}
<c_comment>.                        /* skipping comment body */
<c_comment>[\r\n]                   /* skipping comment eol */
<c_comment><<EOF>>             {
    das_yyerror("end of file encountered inside c-style comment", CompilationError::comment_contains_eof);
    BEGIN(normal);
}
<strb>\"                {
    // assert(nested_sb==0);
    BEGIN(normal);
    return END_STRING;
}
<strb>\{                {
    DAS_ASSERT(das_nested_sb==0);
    das_nested_sb ++;
    BEGIN(normal);
	return BEGIN_STRING_EXPR;
}
<strb><<EOF>>             {
    das_yyerror("string constant exceeds file", CompilationError::string_constant_exceeds_file);
    BEGIN(normal);
    return END_STRING;
}
<strb>\\[\{\"\}]        {
    das_yylval.ch = yytext[1];
    return STRING_CHARACTER;
}
<strb>[\r]              /* do exactly nothing */
<strb>[\n\t]              {
    das_yylval.ch = *yytext;
    return STRING_CHARACTER;
}
<strb>.                 {
    das_yylval.ch = *yytext;
    return STRING_CHARACTER;
}
<indent>[ \t\r]*\n		/* skip empty line */ {
    das_current_line_indent = 0;
}
<indent>" "             { 
	das_current_line_indent++; 
	#ifdef FLEX_DEBUG
		printf("[ ], indent=%i\n", das_current_line_indent);
	#endif
}
<indent>"\t"            { 
	das_current_line_indent = (das_current_line_indent + das_tab_size) & ~(das_tab_size-1);  
	#ifdef FLEX_DEBUG
		printf("\\t, cli=%i\n", das_current_line_indent);
	#endif
}
<indent>(\/\/.*)*\n     { 
	das_current_line_indent = 0; 
	das_need_oxford_comma = true; 
	das_yycolumn = 1; 
	#ifdef FLEX_DEBUG
		printf("new line\n");
	#endif
}
<indent>.               {
    unput(*yytext);
    if (das_current_line_indent > das_indent_level*das_tab_size ) {
        das_indent_level++;
        #ifdef FLEX_DEBUG
        printf("emit {, cli=%i, indent =%i\n", das_current_line_indent, das_indent_level);
        #endif
        return '{';
    } else if (das_current_line_indent < das_indent_level*das_tab_size ) {
        das_indent_level--;
        #ifdef FLEX_DEBUG
        printf("emit }, cli=%i, indent =%i\n", das_current_line_indent, das_indent_level);
        #endif
        das_yycolumn--;
        return '}';
    } else {
        BEGIN(normal);
    }
}
<indent><<EOF>>         {
    if ( g_FileAccessStack.size()==1 ) {
        if ( das_indent_level ) {
            das_indent_level--;
            unput('\r');
            #ifdef FLEX_DEBUG
            printf("emit }\n");
            #endif
            return '}';
        } else {
            return 0;
        }
    } else {
        yypop_buffer_state();
        g_FileAccessStack.pop_back();
        yylineno = das_line_no.back();
        das_line_no.pop_back();
    }
}

<include>[ \t]*                     /* eat the whitespace */
<include>[^ \t\r\n]+                { /* got the include file name */
    auto cfi = g_FileAccessStack.back();
    string incFileName = g_Access->getIncludeFileName(cfi->name,yytext);
    auto info = g_Access->getFileInfo(incFileName);
    if ( !info ) {
        das_yyerror("can't open "+incFileName);
    } else {
        if ( das_already_include.find(incFileName) == das_already_include.end() ) {
            das_already_include.insert(incFileName);
            g_FileAccessStack.push_back(info);
            das_line_no.push_back(yylineno);
            yylineno = 1;
            yypush_buffer_state(YY_CURRENT_BUFFER);
            yy_scan_bytes(info->source, info->sourceLength);
        }
    }
    BEGIN(normal);
}

<normal>"include"                                       BEGIN(include);
<normal>"for"                                           /* das_need_oxford_comma = false; */ return DAS_FOR;
<normal>"while"                                         das_need_oxford_comma = false; return DAS_WHILE;
<normal>"if"                                            das_need_oxford_comma = false; return DAS_IF;
<normal>"elif"                                          das_need_oxford_comma = false; return DAS_ELIF;
<normal>"else"                                          das_need_oxford_comma = false; return DAS_ELSE;
<normal>"finally"                                       das_need_oxford_comma = false; return DAS_FINALLY;
<normal>"def"                                           das_need_oxford_comma = false; return DAS_DEF;
<normal>"with"                                          das_need_oxford_comma = false; return DAS_WITH;
<normal>"let"[ \t\r]*\/\/.*\n                           das_need_oxford_comma = false; unput('\n'); return DAS_LET;
<normal>"let"[ \t\r]*\(                                 das_need_oxford_comma = false; unput('(');  return DAS_LET;
<normal>"let"                                           return DAS_LET;
<normal>"var"[ \t\r]*\/\/.*\n                           das_need_oxford_comma = false; unput('\n'); return DAS_VAR;
<normal>"var"[ \t\r]*\(                                 das_need_oxford_comma = false; unput('(');  return DAS_VAR;
<normal>"var"                                           return DAS_VAR;
<normal>"struct"                                        das_need_oxford_comma = false; return DAS_STRUCT;
<normal>"enum"                                          das_need_oxford_comma = false; return DAS_ENUM;
<normal>"try"                                           das_need_oxford_comma = false; return DAS_TRY;
<normal>"recover"                                       das_need_oxford_comma = false; return DAS_CATCH;
<normal>"typedef"                                       das_need_oxford_comma = false; return DAS_TYPEDEF;
<normal>"label"                                         return DAS_LABEL;
<normal>"goto"                                          return DAS_GOTO;
<normal>"module"                                        das_need_oxford_comma = false; return DAS_MODULE;
<normal>"public"                                        return DAS_PUBLIC;
<normal>"options"                                       return DAS_OPTIONS;
<normal>"operator"                                      return DAS_OPERATOR;
<normal>"require"                                       return DAS_REQUIRE;
<normal>"block"                                         return DAS_TBLOCK;
<normal>"function"                                      return DAS_TFUNCTION;
<normal>"lambda"                                        return DAS_TLAMBDA;
<normal>"tuple"                                         return DAS_TTUPLE;
<normal>"const"                                         return DAS_CONST;
<normal>"continue"                                      return DAS_CONTINUE;
<normal>"where"                                         return DAS_WHERE;
<normal>"cast"                                          return DAS_CAST;
<normal>"upcast"                                        return DAS_UPCAST;
<normal>"pass"                                          return DAS_PASS;
<normal>"reinterpret"                                   return DAS_REINTERPRET;
<normal>"override"                                      return DAS_OVERRIDE;
<normal>"expect"                                        return DAS_EXPECT;
<normal>"table"                                         return DAS_TABLE;
<normal>"array"                                         return DAS_ARRAY;
<normal>"iterator"                                      return DAS_ITERATOR;
<normal>"in"                                            return DAS_IN;
<normal>"implicit"                                      return DAS_IMPLICIT;
<normal>"is"                                            return DAS_IS;
<normal>"deref"                                         return DAS_DEREF;
<normal>"addr"                                          return DAS_ADDR;
<normal>"scope"                                         return DAS_SCOPE;
<normal>"null"                                          return DAS_NULL;
<normal>"return"                                        return DAS_RETURN;
<normal>"break"                                         return DAS_BREAK;
<normal>"typeinfo"                                      return DAS_TYPEINFO;
<normal>"type"                                          return DAS_TYPE;
<normal>"new"                                           return DAS_NEWT;
<normal>"delete"                                        return DAS_DELETE;
<normal>"true"                                          return DAS_TRUE;
<normal>"false"                                         return DAS_FALSE;
<normal>"auto"                                          return DAS_TAUTO;
<normal>"bool"                                          return DAS_TBOOL;
<normal>"void"                                          return DAS_TVOID;
<normal>"string"                                        return DAS_TSTRING;
<normal>"range"                                         return DAS_TRANGE;
<normal>"int"                                           return DAS_TINT;
<normal>"int8"                                          return DAS_TINT8;
<normal>"int16"                                         return DAS_TINT16;
<normal>"int64"                                         return DAS_TINT64;
<normal>"int2"                                          return DAS_TINT2;
<normal>"int3"                                          return DAS_TINT3;
<normal>"int4"                                          return DAS_TINT4;
<normal>"uint"                                          return DAS_TUINT;
<normal>"uint8"                                         return DAS_TUINT8;
<normal>"uint16"                                        return DAS_TUINT16;
<normal>"uint64"                                        return DAS_TUINT64;
<normal>"uint2"                                         return DAS_TUINT2;
<normal>"uint3"                                         return DAS_TUINT3;
<normal>"uint4"                                         return DAS_TUINT4;
<normal>"double"                                        return DAS_TDOUBLE;
<normal>"float"                                         return DAS_TFLOAT;
<normal>"float2"                                        return DAS_TFLOAT2;
<normal>"float3"                                        return DAS_TFLOAT3;
<normal>"float4"                                        return DAS_TFLOAT4;
<normal>[_[:alpha:]][_[:alnum:]]*                       das_yylval.s = new string(yytext);  return NAME;    // TODO: track allocations
<normal>\"                                  {
        BEGIN(strb);
        return BEGIN_STRING;
    }

<normal>\'.\'                           das_yylval.i = int32_t(yytext[1]); return INTEGER;

<normal>[0-9]+(u|U)(l|L)                return sscanf(yytext, "%" SCNu64, &das_yylval.ui64)!=1 ? LEXER_ERROR : UNSIGNED_LONG_INTEGER;
<normal>[0-9]+(l|L)                     return sscanf(yytext, "%" SCNi64, &das_yylval.i64)!=1 ? LEXER_ERROR : LONG_INTEGER;
<normal>[0-9]+(u|U)                     return sscanf(yytext, "%u",  &das_yylval.ui)!=1 ? LEXER_ERROR : UNSIGNED_INTEGER;
<normal>[0-9]+                          {
		int64_t int_const;
		if ( sscanf(yytext, "%" SCNi64,  &int_const)!=1 ) {
			return LEXER_ERROR;
		} else {
			if ( int_const<INT32_MIN || int_const>INT32_MAX ) {
				das_yyerror("integer constant out of range", CompilationError::integer_constant_out_of_range);
			}
			das_yylval.i = int32_t(int_const);
			return INTEGER;
		}
	}	

<normal>0[xX][0-9a-fA-F]+(u|U)(l|L)     return sscanf(yytext, "%" SCNx64, &das_yylval.ui64)!=1 ? LEXER_ERROR : UNSIGNED_LONG_INTEGER;
<normal>0[xX][0-9a-fA-F]+(l|L)          return sscanf(yytext, "%" SCNx64, &das_yylval.ui64)!=1 ? LEXER_ERROR : UNSIGNED_LONG_INTEGER;

<normal>0[xX][0-9a-fA-F]+(u|U)          {
		uint64_t int_const;
		if ( sscanf(yytext, "%" SCNx64,  &int_const)!=1 ) {
			return LEXER_ERROR;
		} else {
			if ( int_const>UINT32_MAX ) {
				das_yyerror("integer constant out of range", CompilationError::integer_constant_out_of_range);
			}
			das_yylval.ui = uint32_t(int_const);
			return UNSIGNED_INTEGER;
		}
	}
		
<normal>0[xX][0-9a-fA-F]+               {
		uint64_t int_const;
		if ( sscanf(yytext, "%" SCNx64,  &int_const)!=1 ) {
			return LEXER_ERROR;
		} else {
			if ( int_const>UINT32_MAX ) {
				das_yyerror("integer constant out of range", CompilationError::integer_constant_out_of_range);
			}
			das_yylval.ui = uint32_t(int_const);
			return UNSIGNED_INTEGER;
		}
	}

<normal>([0-9]*)?\.[0-9]+([eE][+\-]?[0-9]+)?(f|F)?      return sscanf(yytext, "%lf", &das_yylval.fd)!=1 ? LEXER_ERROR : FLOAT;
<normal>[0-9][0-9]*\.[0-9]+?([eE][+\-]?[0-9]+)?(f|F)?   return sscanf(yytext, "%lf", &das_yylval.fd)!=1 ? LEXER_ERROR : FLOAT;
<normal>[0-9]+(f|F)                                     return sscanf(yytext, "%lf", &das_yylval.fd)!=1 ? LEXER_ERROR : FLOAT;
<normal>[0-9]+[eE][+\-]?[0-9]+(f|F)?                    return sscanf(yytext, "%lf", &das_yylval.fd)!=1 ? LEXER_ERROR : FLOAT;

<normal>([0-9]*)?\.[0-9]+([eE][+\-]?[0-9]+)?lf			return sscanf(yytext, "%lf", &das_yylval.d)!=1 ? LEXER_ERROR : DOUBLE;
<normal>[0-9][0-9]*\.[0-9]+?([eE][+\-]?[0-9]+)?lf		return sscanf(yytext, "%lf", &das_yylval.d)!=1 ? LEXER_ERROR : DOUBLE;
<normal>[0-9]+lf                                        return sscanf(yytext, "%lf", &das_yylval.d)!=1 ? LEXER_ERROR : DOUBLE;
<normal>[0-9]+[eE][+\-]?[0-9]+lf                        return sscanf(yytext, "%lf", &das_yylval.d)!=1 ? LEXER_ERROR : DOUBLE;
<normal>\)                                  {
    if ( !das_nested_parentheses ) {
        das_yyerror("mismatching parentheses", CompilationError::mismatching_parentheses);
        return LEXER_ERROR;
    }
    das_nested_parentheses --;
    return ')';
}
<normal>\(                                  {
    das_nested_parentheses ++;
    return '(';
}
<normal>\]                                  {
    if ( !das_nested_square_braces ) {
        das_yyerror("mismatching square braces", CompilationError::mismatching_parentheses);
        return LEXER_ERROR;
    }
    das_nested_square_braces --;
    return ']';
}
<normal>\[                                  {
    das_nested_square_braces ++;
    return '[';
}
<normal>\}                                  {
    if ( das_nested_sb ) {
        das_nested_sb --;
        if ( !das_nested_sb ) {
            BEGIN(strb);
			return END_STRING_EXPR;
        } else {
            return '}';
        }
    } else {
        if ( !das_nested_curly_braces ) {
            das_yyerror("mismatching curly braces", CompilationError::mismatching_curly_bracers);
            return LEXER_ERROR;
        }
        das_nested_curly_braces --;
        return '}';
    }
}
<normal>\{                                  {
    if ( das_nested_sb ) {
        das_nested_sb ++;
    } else {
        das_nested_curly_braces ++;
    }
    return '{';
}
<normal>"\:\:"                              return COLCOL;
<normal>"\|\>"                              return RPIPE;
<normal>\<\|[ \t\r]*\/\/.*\n                das_need_oxford_comma = false; unput('\n'); return LBPIPE;
<normal>\<\|[ \t\r]*\n                      das_need_oxford_comma = false; unput('\n'); return LBPIPE;
<normal>\<\|[ \t]\$                         {
    unput('$');
    if ( das_nested_parentheses ) {
        return LPIPE;
    } else {
        das_need_oxford_comma = false;
        return LBPIPE;
    }
}
<normal>"\<\|"                              return LPIPE;
<normal>"\?\?"                              return QQ;
<normal>"\?\."                              return QDOT;
<normal>"\:\="                              return CLONEEQU;
<normal>"\-\>"                              return RARROW;
<normal>"\<\-"                              return LARROW;
<normal>"\+\="                              return ADDEQU;
<normal>"\-\="                              return SUBEQU;
<normal>"\/\="                              return DIVEQU;
<normal>"\*\="                              return MULEQU;
<normal>"\%\="                              return MODEQU;
<normal>"\&\="                              return ANDEQU;
<normal>"\|\="                              return OREQU;
<normal>"\^\="                              return XOREQU;
<normal>"\+\+"                              return ADDADD;
<normal>"\-\-"                              return SUBSUB;
<normal>"\<\="                              return LEEQU;
<normal>"\>\="                              return GREQU;
<normal>"\=\="                              return EQUEQU;
<normal>"\!\="                              return NOTEQU;
<normal>"\>\>\>"                            return ROTR;
<normal>"\<\<\<"                            return ROTL;
<normal>"\>\>"                              return SHR;
<normal>"\<\<"                              return SHL;
<normal>"\>\>\="                            return SHREQU;
<normal>"\<\<\="                            return SHLEQU;
<normal>"\>\>\>\="                          return ROTREQU;
<normal>"\<\<\<\="                          return ROTLEQU;
<normal>"\=\>"                              return MAPTO;
<normal>"\[\["                              {
		das_nested_square_braces ++;
		das_nested_square_braces ++;
		return BRABRAB;
	}
<normal>"\[\{"                              {
		das_nested_square_braces ++;
		das_nested_curly_braces ++;
		return BRACBRB;
	}
<normal>"\{\{"                              {
		das_nested_curly_braces ++;
		das_nested_curly_braces ++;
		return CBRCBRB;
	}
<normal>[ \t\r]                             /* skip white space */
<normal>(\/\/.*)*\n                        {
    das_yycolumn = 1;
    if  ( !das_nested_parentheses && !das_nested_curly_braces && !das_nested_square_braces ) {
        bool ns = (das_current_line_indent!=0) && das_need_oxford_comma;
        das_current_line_indent = 0;
        das_need_oxford_comma = true;
        BEGIN(indent);
        #ifdef FLEX_DEBUG
        printf("emit ;\n");
        #endif
        if ( ns )
            return ';';
    }
}
<normal><<EOF>>         {
    if ( g_FileAccessStack.size()==1 ) {
        das_yycolumn = 1;
        if  ( !das_nested_parentheses && !das_nested_curly_braces && !das_nested_square_braces ) {
            bool ns = (das_current_line_indent!=0) && das_need_oxford_comma;
            das_current_line_indent = 0;
            das_need_oxford_comma = true;
            BEGIN(indent);
            #ifdef FLEX_DEBUG
            printf("emit ;\n");
            #endif
            if ( ns ) {
                return ';';
            }
        } else {
            return 0;
        }
    } else {
        yypop_buffer_state();
        g_FileAccessStack.pop_back();
        yylineno = das_line_no.back();
        das_line_no.pop_back();
    }
}
<normal>.                                   return *yytext;

%%

extern int das_yydebug;

void das_yybegin(const char * str) {
    das_already_include.clear();
    das_yydebug = 0;
    yylineno = 1;
	das_tab_size = das_def_tab_size;
    das_line_no.clear();
    das_yycolumn = 1;
    das_current_line_indent = 0;
    das_indent_level = 0;
    das_nested_parentheses = 0;
    das_nested_curly_braces = 0;
	das_nested_square_braces = 0;
    das_nested_sb = 0;
    das_need_oxford_comma = true;
    das_c_style_depth = 0;
    BEGIN(normal);
    yy_scan_string(str);
}


